
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.7'
    id 'org.ajoberstar.grgit' version '1.3.2'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply plugin: "org.ajoberstar.grgit"

ext.git = grgit.open(file('.'))

ext {
    configFile = file "build.properties"

    revision = git.head().abbreviatedId
    depth = git.log().size()
}


configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    //noinspection GrUnresolvedAccess
    ext.config = new ConfigSlurper().parse prop
}

group= "portablejim.veinminer"
version = config.mod_version
archivesBaseName = "VeinMiner"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    mappings = "snapshot_20160518"
    runDir = "run"
}

String buildNumber = "unknown"
if(System.getenv("BUILD_NUMBER") != null) {
    buildNumber = System.getenv("BUILD_NUMBER")
}

version = "${config.mod_version}-${config.minecraft_version}r${ext.depth}+${ext.revision}"

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'version.properties'

        // replace version and mcversion
        expand 'version':project.config.mod_version, 'mcversion':project.config.minecraft_version, 'gitrev': revision,  'revnumber': depth
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'version.properties'
    }
}

ext.sharedmanifest = manifest {
        //attributes 'FMLCorePlugin': 'portablejim.veinminer.asm.VeinMinerCorePlugin'
        //attributes 'FMLCorePluginContainsFMLMod': 'true'
}

jar {
    manifest = project.manifest {
        from sharedmanifest
    }
}

sourceCompatibility = 6
targetCompatibility = 6

task apiJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'api'
    include "portablejim/veinminer/api/**"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    manifest = project.manifest {
        from sharedmanifest
    }
    classifier = 'dev'
}

//noinspection GrUnresolvedAccess
task release(type: Copy) {
    dependsOn "build"

    from project.tasks.jar.archivePath
    into "release/"
}

runClient {
    jvmArgs "-Dfml.coreMods.load=portablejim.veinminer.asm.VeinMinerCorePlugin"
}

build.dependsOn apiJar, deobfJar

artifacts {
    archives deobfJar
}

configurations {
    deployerJars
}

uploadArchives {
    dependsOn 'reobf'

    repositories {
        if(project.hasProperty("pushMaven")) {
            logger.info("Pushing to maven repo")

            mavenDeployer {
                configuration = configurations.deployerJars

                repository(url: "file://localhost/home/public_html/maven.jamesmckay.id.au/htdocs/")

                pom {
                    groupId = project.group
                    version = project.version
                    artifactId = project.archivesBaseName
                    packaging = 'jar'
                    description 'VeinMiner Mod'
                }
            }
        }
    }
}
